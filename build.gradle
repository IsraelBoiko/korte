buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven { url 'http://dl.bintray.com/kotlin/kotlin-eap-1.2' }
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "com.jtransc:jtransc-gradle-plugin:$jtranscVersion"
    }

    ext {
        libraries = [
                kotlin_stdlib_common: "org.jetbrains.kotlin:kotlin-stdlib-common:$kotlinVersion",
                kotlin_stdlib_jvm   : "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion",
                kotlin_stdlib_js    : "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlinVersion",

                kotlin_test_common  : "org.jetbrains.kotlin:kotlin-test-common:$kotlinVersion",
                kotlin_test_jvm     : "org.jetbrains.kotlin:kotlin-test:$kotlinVersion",
                kotlin_test_js      : "org.jetbrains.kotlin:kotlin-test-js:$kotlinVersion",

                kotlin_reflect      : "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion",
        ]
    }
}

configurations {
    provided
}

def file = new File(".").absolutePath

allprojects {
    group 'com.soywiz'
    version "$korVersion"

    apply plugin: 'java'
    //apply plugin: 'kotlin'
    apply plugin: 'maven'
    apply plugin: 'signing'
    apply plugin: 'maven-publish'

    //kotlin {
    //    experimental { coroutines 'enable' }
    //}

    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    configurations {
        provided
    }

    repositories {
        mavenLocal()
        jcenter()
        maven { url 'http://dl.bintray.com/kotlin/kotlin-eap-1.2' }
        mavenCentral()
    }

    it.afterEvaluate {
        if (it.plugins.hasPlugin("kotlin-platform-common")) {
            dependencies {
                compile libraries.kotlin_stdlib_common
                testCompile libraries.kotlin_test_common
            }

            kotlin {
                experimental { coroutines 'enable' }
            }
        }
        if (it.plugins.hasPlugin("kotlin-platform-jvm") || it.plugins.hasPlugin("kotlin")) {
            dependencies {
                compile libraries.kotlin_stdlib_jvm
                testCompile libraries.kotlin_test_jvm
                testCompile "junit:junit:4.12"
            }

            kotlin {
                experimental { coroutines 'enable' }
            }
        }
        if (it.plugins.hasPlugin("kotlin-platform-js") || it.plugins.hasPlugin("kotlin2js")) {
            dependencies {
                compile libraries.kotlin_stdlib_js
                testCompile libraries.kotlin_test_js
            }

            kotlin {
                experimental { coroutines 'enable' }
            }

            compileKotlin2Js {
                kotlinOptions.moduleKind = "umd"
                kotlinOptions.sourceMap = true
            }
        }
    }

    //dependencies {
    //    compile "com.soywiz:korio:$korVersion"
    //    testCompile group: 'junit', name: 'junit', version: '4.11'
    //    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    //}

    task javadoc2(type: Javadoc) {
        failOnError = false
    }

    task javadocJar(type: Jar, dependsOn: javadoc2) {
        classifier = 'javadoc'
        from 'build/docs/javadoc'
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        classifier = 'sources'
    }

    artifacts {
        archives jar
        archives javadocJar
        archives sourcesJar
    }

// gradle uploadArchives
    if (project.hasProperty('sonatypeUsername')) {
        signing {
            sign configurations.archives
        }

        uploadArchives {
            repositories {
                mavenDeployer {
                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                    repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                        authentication(userName: project.sonatypeUsername, password: project.sonatypePassword)
                    }

                    pom.project {
                        name "${project.name}"
                        packaging 'jar'
                        description 'korte: Kotlin cORoutines Template Engine'
                        url 'https://github.com/soywiz/korte/'
                        inceptionYear '2017'

                        scm {
                            url 'scm:git@github.com:soywiz/korte.git'
                            connection 'scm:git@github.com:soywiz/korte.git'
                            developerConnection 'scm:git@github.com:soywiz/korte.git'
                        }

                        licenses {
                            license {
                                name 'The Apache Software License, Version 2.0'
                                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                                distribution 'repo'
                            }
                        }

                        developers {
                            developer {
                                id 'soywiz'
                                name 'Carlos Ballesteros Velasco'
                            }
                        }
                    }
                }
            }
        }
    }

    publishing {
        publications {
            MyPublication(MavenPublication) {
                from components.java
                groupId project.group
                artifactId project.name
                version "$project.version"
            }
        }
    }

    task deploy(dependsOn: ['install', 'uploadArchives']) {
    }
}
